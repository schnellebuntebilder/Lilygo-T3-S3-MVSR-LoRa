/*
 * @Description: SX127x_PingPong_2 test
 * @Author: LILYGO_L
 * @Date: 2024-12-02 18:06:13
 * @LastEditTime: 2025-02-05 17:07:56
 * @License: GPL 3.0
 */
#include "RadioLib.h"
#include "pin_config.h"
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, -1);

// uncomment the following only on one
// of the nodes to initiate the pings
// #define INITIATING_NODE

static const uint64_t Local_MAC = ESP.getEfuseMac();

static size_t CycleTime = 0;

#ifdef T3_S3_SX1276
SX1276 radio = new Module(LORA_CS, LORA_DIO0, LORA_RST, LORA_DIO1, SPI);
#endif

#ifdef T3_S3_SX1278
SX1278 radio = new Module(LORA_CS, LORA_DIO0, LORA_RST, LORA_DIO1, SPI);
#endif

uint8_t Receive_Package[16];
uint32_t Receive_Data = 0;

uint8_t Send_Package[16] = {'M', 'A', 'C', ':',
                            (uint8_t)(Local_MAC >> 56), (uint8_t)(Local_MAC >> 48),
                            (uint8_t)(Local_MAC >> 40), (uint8_t)(Local_MAC >> 32),
                            (uint8_t)(Local_MAC >> 24), (uint8_t)(Local_MAC >> 16),
                            (uint8_t)(Local_MAC >> 8), (uint8_t)Local_MAC,
                            0, 0, 0, 0};

uint32_t Send_Data = 0;
bool Lora_Mode = 1;

volatile bool Lora_Receive_Flag = false;

// ── Display helper ───────────────────────────────────────────────
// Print up to 4 lines on the OLED. Pass nullptr to skip a line.
void displayPrint(const char *l1, const char *l2 = nullptr,
                  const char *l3 = nullptr, const char *l4 = nullptr)
{
    display.clearDisplay();
    display.setTextSize(1);
    display.setTextColor(SSD1306_WHITE);
    display.setCursor(0, 0);
    if (l1) { display.println(l1); }
    if (l2) { display.println(l2); }
    if (l3) { display.println(l3); }
    if (l4) { display.println(l4); }
    display.display();
}

void Lora_Receive_Flag_Callback(void)
{
    Lora_Receive_Flag = true;
}

void setup()
{
    Serial.begin(115200);

    Wire.begin(SCREEN_SDA, SCREEN_SCL);
    if (!display.begin(SSD1306_SWITCHCAPVCC, SCREEN_ADDRESS)) {
        Serial.println("SSD1306 init failed");
    }
    displayPrint("SX127x PingPong2", "Initializing...");

    // initialize SX1276 with default settings
    Serial.println("[SX1276] Initializing ... ");
    SPI.begin(LORA_SCLK, LORA_MISO, LORA_MOSI);
    int state = radio.begin();
    if (state == RADIOLIB_ERR_NONE)
    {
        Serial.println("success!");
        displayPrint("SX127x PingPong2", "LoRa OK", "Mode: LISTEN");
    }
    else
    {
        Serial.print("failed, code ");
        Serial.println(state);
        char buf[32];
        snprintf(buf, sizeof(buf), "Error code: %d", state);
        displayPrint("SX127x PingPong2", "LoRa FAILED", buf);
        while (true)
            ;
    }

    // radio.setFrequency(914.9);
    radio.setFrequency(868.1);
    radio.setBandwidth(500.0);
    // radio.setBitRate(300.0);
    radio.setSpreadingFactor(9);
    radio.setCodingRate(6);
    radio.setSyncWord(0xAB);
    radio.setCurrentLimit(240);
    radio.setOutputPower(17);
    radio.setPreambleLength(16);
    radio.setCRC(false);

    radio.setPacketReceivedAction(Lora_Receive_Flag_Callback);

    radio.startReceive();
}

void loop()
{
    if (digitalRead(0) == LOW)
    {
        delay(300);
        Lora_Mode = !Lora_Mode;

        radio.begin();
        // radio.setFrequency(914.9);
        radio.setFrequency(868.1);
        radio.setBandwidth(500.0);
        // radio.setBitRate(300.0);
        radio.setSpreadingFactor(9);
        radio.setCodingRate(6);
        radio.setSyncWord(0xAB);
        radio.setCurrentLimit(240);
        radio.setOutputPower(17);
        radio.setPreambleLength(16);
        radio.setCRC(false);

        if (Lora_Mode == 1)
        {
            radio.startReceive();
            displayPrint("Mode: LISTEN", "Waiting...");
        }
        else
        {
            displayPrint("Mode: SEND", "Transmitting...");
        }
    }

    if (Lora_Mode == 0)
    {
        if (millis() > CycleTime)
        {
            // send another one
            Serial.println("[SX1276] Sending another packet ... ");

            Send_Package[12] = (uint8_t)(Send_Data >> 24);
            Send_Package[13] = (uint8_t)(Send_Data >> 16);
            Send_Package[14] = (uint8_t)(Send_Data >> 8);
            Send_Package[15] = (uint8_t)Send_Data;

            radio.transmit(Send_Package, 16);

            char buf[32];
            snprintf(buf, sizeof(buf), "TX count: %lu", Send_Data);
            displayPrint("Mode: SEND", "Packet sent!", buf);

            CycleTime = millis() + 100;
        }
    }
    else
    {
        if (Lora_Receive_Flag)
        {
            Lora_Receive_Flag = false;

            if (radio.readData(Receive_Package, 16) == RADIOLIB_ERR_NONE)
            {
                if ((Receive_Package[0] == 'M') &&
                    (Receive_Package[1] == 'A') &&
                    (Receive_Package[2] == 'C') &&
                    (Receive_Package[3] == ':'))
                {
                    uint64_t temp_mac =
                        ((uint64_t)Receive_Package[4] << 56) |
                        ((uint64_t)Receive_Package[5] << 48) |
                        ((uint64_t)Receive_Package[6] << 40) |
                        ((uint64_t)Receive_Package[7] << 32) |
                        ((uint64_t)Receive_Package[8] << 24) |
                        ((uint64_t)Receive_Package[9] << 16) |
                        ((uint64_t)Receive_Package[10] << 8) |
                        (uint64_t)Receive_Package[11];

                    if (temp_mac != Local_MAC)
                    {
                        Receive_Data =
                            ((uint32_t)Receive_Package[12] << 24) |
                            ((uint32_t)Receive_Package[13] << 16) |
                            ((uint32_t)Receive_Package[14] << 8) |
                            (uint32_t)Receive_Package[15];

                        // packet was successfully received
                        Serial.println("[SX1276] Received packet!");

                        // print data of the packet
                        for (int i = 0; i < 16; i++)
                        {
                            Serial.printf("[SX1276] Data[%d]: %#X\n", i, Receive_Package[i]);
                        }

                        // uint32_t temp_mac[2];
                        // temp_mac[0] =
                        //     ((uint32_t)Receive_Package[8] << 24) |
                        //     ((uint32_t)Receive_Package[9] << 16) |
                        //     ((uint32_t)Receive_Package[10] << 8) |
                        //     (uint32_t)Receive_Package[11];
                        // temp_mac[1] =
                        //     ((uint32_t)Receive_Package[4] << 24) |
                        //     ((uint32_t)Receive_Package[5] << 16) |
                        //     ((uint32_t)Receive_Package[6] << 8) |
                        //     (uint32_t)Receive_Package[7];

                        // Serial.printf("Chip Mac ID[0]: %u\n", temp_mac[0]);
                        // Serial.printf("Chip Mac ID[1]: %u\n", temp_mac[1]);

                        // print data of the packet
                        Serial.print("[SX1276] Data:\t\t");
                        Serial.println(Receive_Data);

                        // print RSSI (Received Signal Strength Indicator)
                        Serial.print("[SX1276] RSSI:\t\t");
                        Serial.print(radio.getRSSI());
                        Serial.println(" dBm");

                        // print SNR (Signal-to-Noise Ratio)
                        Serial.print("[SX1276] SNR:\t\t");
                        Serial.print(radio.getSNR());
                        Serial.println(" dB");

                        char dBuf[22], snrBuf[22], dataBuf[22];
                        snprintf(dBuf,    sizeof(dBuf),    "RSSI: %.1f dBm", radio.getRSSI());
                        snprintf(snrBuf,  sizeof(snrBuf),  "SNR:  %.1f dB",  radio.getSNR());
                        snprintf(dataBuf, sizeof(dataBuf), "Data: %lu", Receive_Data);
                        displayPrint("Mode: LISTEN", "Pkt received!", dBuf, snrBuf);

                        Send_Data = Receive_Data + 1;

                        radio.startReceive();
                    }
                }
            }
        }
    }
}
